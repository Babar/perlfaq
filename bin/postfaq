#!/usr/bin/perl -w
use strict;

=head1 NAME

postfaq - post a portion of the perlfaq to a newsgroup

=head1 SYNOPSIS

postfaq [configfile]

=head1 DESCRIPTION

This script posts a portion of the perlfaq to comp.lang.perl.misc.

=head2 Configuration

This script uses these configuration directives:

	SERVER           the news server to post to
	GROUP            the group to post to
	EMAIL            the email address to use in the From field
	FAQ_DIR          the directory with split files (from splitfaq)
	POST_TEMPLATE    the Text::Template file to use
	NNTP_USER        the NNTP user (if needed)
	NNTP_PASS        the NNTP password (if needed)
	
This is a sample configuration file:

	SERVER news.example.com
	GROUP alt.test
	EMAIL joe@example.com
	FAQ_DIR /path/to/splits
	POST_TEMPLATE /path/to/template
	
The script checks the file specified on the command line first.
If you do not specify a file, it looks in the environment variable
POSTFAQ_CONFIG, and finally for ~/.postfaqrc.  It uses the first
configuration filename it finds.

If the file does not exist, is not a plain file, and is not 
readable, the script dies.
		
=head2 Post template

The post template, specified in the POST_TEMPLATE configuration
directive, is a Text::Template file.  The script makes available
the following variables:

	$subject   the question line of the faq 
	$body      the answer to the question
	$major     the section of the perlfaq,  (N in perlfaqN.pod)
	$minor     the number of the answer within the file

If the template file does not exist, is not a plain file, and is not 
readable, the script dies.

=head2 Errors

On any error, the script dies.  It looks for various files, and if
it does not find them, stops the script.

=head1 TO DO

* check the configuration info for all of the necessary pieces
before we start work.

* don't print error messages in non-interactive sessions

* finish the docs :)

=head1 BUGS

* none identified yet.

=head1 SEE ALSO

L<Text::Template>, the splitfaq script in the perlfaq repository

=head1 AUTHOR

brian d foy, C<< <bdfoy@cpan.org> >>

=head1 COPYRIGHT

Copyright 2002, brian d foy

You may use and redistribute this under the same terms as
Perl itself.

=cut

use ConfigReader::Simple;
use File::Spec;
use Net::NNTP;
use Text::Template;

my $config                   = get_config( $ARGV[0] );
                               check_setup( $config );

my $nntp                     = get_nntp( $config );

my ( $file, $major, $minor ) = select_faq( $config );

my( $subject, $body )        = get_content( $file );



my $message = Text::Template::fill_in_file( 
	$config->POST_TEMPLATE,

	HASH =>
		{
		subject  => $subject,
		body     => $body,
		major    => $major,
		minor    => $minor,
		} 
	);
	


$subject = "$major.$minor $subject";

my @header                   = get_header( $config, $subject );

#print "@header", "\n", $message;

$nntp->post( @header, "\n", split /$/m, $message );

$nntp->quit;

# # # # # # # # # # # # # # #
 # # # # # # # # # # # # # # #
# # # # # # # # # # # # # # #

sub get_config
	{
	my $file = shift;
	
	$file ||= $ENV{POSTFAQ_CONFIG} || 
		File::Spec->catfile( $ENV{HOME}, ".postfaqrc");
		
	die "Could not open config file [$file]"
		unless -e $file && -f _ && -r _;
	
	die "Could not open configuration file [$file]"
		unless -e $file && -f _ && -r _;
		
	my $config    = ConfigReader::Simple->new( $file );
	die "Could not parse config file!" unless 
		UNIVERSAL::isa( $config, 'ConfigReader::Simple' );	
		
	$config;	
	}

sub check_setup
	{
	my $config = shift;
	
	die "Could not open faq_dir [" . $config->FAQ_DIR . "]"
		unless check_dir( $config->FAQ_DIR );
	
	die "Could not open template [" . $config->POST_TEMPLATE . "]"
		unless check_file( $config->POST_TEMPLATE );

	die "Could not open history [" . $config->HISTORY . "]"
		unless check_file( $config->POST_TEMPLATE );
	}

sub get_nntp
	{
	my $config = shift;
	
	my $nntp = Net::NNTP->new( $config->SERVER );
	$nntp->postok() or die "Posting not allowed \n";
	$nntp->authinfo( $config->NNTP_USER, $config->NNTP_PASS ) 
		if defined $config->NNTP_USER || defined $config->NNTP_PASS;
	
	$nntp;
	}
	
sub check_file
	{
	my $file = shift;
	
	die "Could not read [$file]" unless -r $file;
	}
	
sub check_dir
	{
	my $dir = shift;
	
	die "Directory does not exist [$dir]" unless -d $dir;
	}
	
sub select_faq
	{
	my $config = shift;
	
	my @files = get_files( $config );
	
	my $file = $files[rand @files];
	utime time, time, $file;
	
	my( $major, $minor ) = $file =~ m/perlfaq.0?([1-9]).0?(\d+).txt$/g;
	
	( $file, $major, $minor );
	}

sub get_files
	{
	my $config = shift;
	
	my $time  = 0;
	
	my @files =
		map  { $_->[0] }
		grep { $time ||= $_->[1]; $_->[1] == $time }
		sort { $b->[1] <=> $a->[1] }
		map  { [ $_, -A $_ ] }
		glob( File::Spec->catfile( $config->FAQ_DIR, "perlfaq*.txt" ) ); 

	@files;
	}
	
sub get_content
	{
	my $file = shift;
	
	open my $fh, $file or die "Could not open FAQ [$file]: $!";
	my $subject = <$fh>;
	my $body    = join "", <$fh>;
	$subject =~ s/^\s+|\s+$//g;
	
	( $subject, $body )
	}
	
sub get_header
	{
	my $config  = shift;
	my $subject = shift;
	
	my @header = map "$_\n", (
		"Newsgroups: " . $config->GROUP,
		"Subject: FAQ $subject",
		"From: PerlFAQ Server <" . $config->EMAIL . ">",
		);
	}
